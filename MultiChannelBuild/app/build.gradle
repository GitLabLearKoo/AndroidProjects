apply plugin: 'com.android.application'

android {
    //apk签名脚本
    signingConfigs {
        config {
            keyAlias 'Edwin'
            keyPassword 'Edwin666666'
            storeFile file('./AppKeyStore.jks')
            storePassword '666666'
        }
    }
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "com.github.why168.multichannelbuild"
        minSdkVersion 11
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"

        //支持lambda
        jackOptions {
            enabled true
        }

        //类似下面productFlavors
        manifestPlaceholders = [
                APP_ID : "APP_ID111111",
                APP_KEY: "APP_KEY222222",
        ]

        //ndk编译
        ndk {
            abiFilters 'armeabi', 'armeabi-v7a', 'arm64-v8a'
        }
    }
    buildTypes {
        debug {
            debuggable true
            buildConfigField "boolean", "LOG_DEBUG", "true"
            minifyEnabled false   //混淆
            shrinkResources false //去除无效的资源文件
            zipAlignEnabled false //Zipalign优化
            signingConfig signingConfigs.config
        }
        release {
            debuggable false
            buildConfigField "boolean", "LOG_DEBUG", "false"
            minifyEnabled true   //混淆
            shrinkResources true //去除无效的资源文件
            zipAlignEnabled true //Zipalign优化
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }
    //源设置
    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }
    //多渠道配置，下面的关键字'CHANNEL_NAME'与AndroidManifest里面必须一致。
    productFlavors {
        baidu {//百度市场
            manifestPlaceholders = [CHANNEL_NAME: 500001];
        }
        yingyongbai {//腾讯应用市场
            manifestPlaceholders = [CHANNEL_NAME: 500002];
        }
        xiaomi {//小米应用市场
            manifestPlaceholders = [CHANNEL_NAME: 500003];
        }
        store360 {//360商店
            manifestPlaceholders = [CHANNEL_NAME: 500004];
        }
    }
    //apk名字自定义
    android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def file = output.outputFile
            output.outputFile = new File(file.parent, file.name.replace(".apk", "-" + defaultConfig.versionName + ".apk"))
        }
    }
    //JDK1.8编译选项
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    //存储库maven仓库设置
    repositories {
        flatDir {
            dirs 'libs'
        }
        jcenter()
        maven { url "https://jitpack.io" }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    aaptOptions {
        cruncherEnabled = false
        useNewCruncher = false
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:25.1.0'
}
